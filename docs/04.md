# MQL 5

## Classi e interfacce

### Classi

Le differenze tra una struttura e una classe sono:

* viene dichiarata tramite la parola chiave `class`,
* ogni membro della classe è di default `private` se non diversamente specificato;
* le classi hanno sempre una tabella di funzioni `virtual` anche se non hanno nessuna funzione `virtual` dichiarata.
* E' possibile usare l'operatore `new` con le classi.
* Le classi si possono estendere **solo** da altre classi.

Le classi e le strutture possono avere un costruttore/distruttore esplicito. Se viene dichiarato l'inizializzazione delle variabili di classe/struttura non si può fare.

```C++
struct settings
    {
        double take;

        settings() { take = 0.0; }
        ~settings() { Print("Distutto"); }
    }

// !! errore non posso farlo
settings my_set = {0.0};
```

#### Costruttori e distruttori

Un costruttore è una funzione speciale che viene chiamata automaticamente nel momento in cui si crea un oggetto o una struttura e viene utilizzato per inizializzare i membri della classe.

Il nome del costruttore deve essere uguale al nome della classe e non restituisce nessun tipo di dato quindi, `void`.

In ogni caso anche se mancasse un costruttore nella classe, i membri di tipo `string`, `object` e `object[]` vengono inizializzati.

Una classe può avere molti costruttori in base al numero di parametri da inizializzare. Il costruttore senza nessun parametro è chiamato `default constructor`.

Il `default constructor` ha un compito speciale quando inizializza un `object[]`.

```C++
class CFoo
    {
        datetime time;

        public:
            void CFoo(const datetime t = 0) { time = t; };
            void CFoo(const CFoo &foo) { time = foo.time; };

            string ToString() { return (TimeToString(time, TIME_DATE|TIME_SECONDS));};
    };

void OnStart()
    {
        // Non posso usare questa variate per inizializzare la classe
        // perchè non c'è un default constructor anche se in questo caso verrebbe richiamato il costruttore parametrico con i valori di default
        // CFoo foo;

        // posso:
        CFoo foo1(TimeCurrent()); // richiamo in modo esplicito il costruttore
        CFoo foo2();              // richiamo in modo esplicito il costruttore senza parametri
        CFoo foo3=D'2009.09.09';  // richiamo in modo implicito il costruttore
        CFoo foo4(foo1);          // richiamo in modo esplicito il secondo costruttore
        CFoo foo5=foo1;           // richiamo in modo implicito il secondo costruttore
        CFoo foo6;                // richiamo in modo esplicito un default constructor, se non esiste viene richiamato un costruttore parametrico con valori di default

        // per quanto riguarda i puntatori:
        CFoo *pfoo1 = new CFoo(); // creazione dinamica di un oggetto e ricezione di un puntatore
        CFoo *pfoo2 = new CFoo(TimeCurrent()); // altra opzione di creazione dinamica di oggetti
        CFoo *pfoo3 = GetPointer(foo1); // in questo caso pfoo3 punta a foo1
        CFoo *pfoo4 = pfoo2; // pfoo4 e pfoo2 puntano allo stesso oggetto

        // non posso fare
        // CFoo a[3]; // perchè non ho definito un default constructor anche se in questo caso verrebbe richiamato il costruttore parametrico con i valori di default

        Print("foo1 = ", foo1.ToString());
        Print("foo2 = ", foo2.ToString());
        Print("foo3 = ", foo3.ToString());
        Print("foo4 = ", foo4.ToString());
        Print("foo5 = ", foo5.ToString());
        Print("foo6 = ", foo6.ToString());

        Print("pfoo1 = ", pfoo1.ToString());
        Print("pfoo2 = ", pfoo2.ToString());
        Print("pfoo3 = ", pfoo3.ToString());
        Print("pfoo4 = ", pfoo4.ToString());

        // elimina i puntatori
        delete pfoo1;
        delete pfoo2;
    }
```

**Nota:** definisci sempre un `default constructor` cosi puoi usare un `object[]` senza problemi. Ogni volta che provi a dichiarare un `object[]` il linguaggio assume che tutti i membri possono essere creati e inizializzati in modo automatico.

#### Liste di inizializzazione

Sintassi speciale che serve ad inizializzare un oggetto durante la sua costruzione. Si tratta di una lista di inizializzatori separati da una virgola.

* Le liste si possono usare solo nei costruttori.
* I membri delle classi padre non si possono inizializzare nella lista.
* Le liste devono essere seguite da una definizione di una funzione.
* L'ordine in cui vengono scritti i membri è l'ordine di inizializzazione.
* Posso richiamare il costruttore di una classe padre all'interno della lista.

```C++
class CPerson
    {
        string m_first_name;
        string m_last_name;

        public:
            CPerson() { Print(__FUNCTION__); };
            CPerson(string full_name);
            CPerson(string surname, string name): m_first_name(surname), m_last_name(name) {};

            void PrintName() { PrintFormat("Name: %s, Last: %s", m_first_name, m_last_name); };
    };

CPerson::CPerson(string full_name)
    {
        int pos = StringFind(full_name, " ");

        if (pos >= 0) {
            m_first_name = StringSubstr(full_name, 0, pos);
            m_last_name = StringSubstr(full_name, pos+1);
        }
    }

void OnStart()
    {
        CPerson people[5]; // ok, ho un default constructor

        CPerson silvia = "Silvia Sarti";
        CPerson andrea("Andrew", "Loyd");
        CPerson *tom = new CPerson("Tom", "Jones");

        silvia.PrintName();
        andrea.PrintName();
        tom.PrintName();

        // elimina sempre i puntatori
        delete tom;
    }
```

**Distruttori**

Il distruttore si scrive come il nome della classe preceduto da un tilde (~). I membri di tipo `string`, `object` e `object[]` richiedono una distruzione esplicita ma nel caso vengono sempre distrutti. Un distruttore è **sempre** una funzione `virtual` che sia dichiarato in modo esplicito oppure no.

### Definizione dei metodi di una classe

I metodi della classe si possono definire sia all'interno che all'esterno della definizione della classe stessa. Nel caso vengano definiti nella classe stessa (all'interno) il body del metodo viene dichiarato appena a destra del suo identificatore.

```C++
class CFoo
    {
        protected:
            int m_type;
        public:
            CFoo();
            int GetType() { return(m_type); } // metodi semplici
            bool Check(); // metodo complesso da definire all'esterno
    };

bool CFoo::Check()
    {
        // body
        return(true);
    }
```

#### Public, Protected e Private e la sprecifica delete

L'accesso ai membri di una classe funziona come in tutte le altre classi.

E' possibile forzare un membro o metodo a non essere utilizzato e a ritornare un errore già in fase di compilazione se utilizzato. La specifica `delete` serve proprio a questo.

```C++
class A
    {
        public:
            A(void) { value=5; };
            double GetValue(void) { return(value); }
        private:
            double value;
    };

class B: public A
    {
        double GetValue(void)=delete;
    };
```

Inoltre, `delete` permette di disabilitare l'`auto-casting` o la copia dei costruttori che altrimenti dovrebbe essere nascosta nella sezione `private`.

```C++
class A
    {
        private:
            double value;

        public:
            void SetValue(double v) { value = v; }

            // disabilito il call tramite valori di tipo int
            void SetValue(int v) = delete;
            // disabilito il copy-operator
            void operator = (const A&) = delete;
    }
```

### Interfacce

Una interfaccia permette di determinare delle funzionalità specifiche che una classe deve implementare. Infatti una interfaccia non può contenere membri e non ha costruttori/distruttori. Tutti i metodi di una interfaccia sono puramente `virtual`.

```C++
interface IAnimal
    {
        void virtual Sound();
        void Walk(); // questo metodo è comunque virtual
    };
```

Come le `abstract class` una interfaccia non può essere istanziata. Le interfacce sono sempre visibili pubblicamente.

**Nota:** Un puntatore ad una interfaccia si può salvare in una variabile di tipo `void *` come in generale per tutti gli oggetti. Per convertire un puntatore di tipo `void *` in una classe specifica è necessario usare l'operatore di `cast` dinamico.
